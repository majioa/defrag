.MODEL	SMALL
.286
.STACK	100H
COD	SEGMENT PARA
ASSUME	CS:COD,DS:DAT,SS:STACK
BPB	EQU	11
AT	EQU	PUT_CURSOR
DATS	EQU	CS:[DATSEG]
BOOTS	EQU	CS:[BOOTSEG]
FATS	EQU	CS:[FATSEG]
ROOTS	EQU	CS:[ROOTSEG]
BUFS	EQU	CS:[BUFSEG]
BUF1S	EQU	CS:[BUF1SEG]
CLUSS	EQU	CS:[CLUSSEG]
FAT	EQU	BOOT+512/16
ROOT	EQU	FAT
BUF	EQU	FAT
BUF1	EQU	FAT+1000H
CLUS	EQU	BUF1+1000H
DEFRAG	PROC
	PUSH	DS
	POP	SS
DEFRAG_I:
	MOV	DS,DATS
	PUSH	DS
	POP	ES
	CALL	CLS
	CALL	DETECT_DISKS
	OR	DL,DL
	JZ	NO_DISKS
	CALL	CHANGE_DISK;(A OR B)OUT AX
	JNC	DEFRAG_EXIT
	MOV	DS:[CUR_DISK],AX
	CALL	DEFRAGP
DEFRAG_EXIT:
	MOV	AX,4C00H
	INT	21H
NO_DISKS:
	MOV	AX,0A00H+(80-24)/2
	LEA	SI,NO_DISKS_
	CALL	WW
	JMP	SHORT	DEFRAG_EXIT
ENDP
DETECT_DISKS	PROC
	MOV	AX,0800H
	MOV	DL,AL
	INT	13H
	MOV	DS:[DISKS],DL
	RET
ENDP
WRITE_ERROR	PROC
	MOV	DS,DATS
	MOV	AX,23*256+(80-13)/2
	CALL	AT
	LEA	SI,NOT_READY
	CALL	WW
	CALL	INKEY
	CALL	CLRSTR
	RET
ENDP
CLS	PROC
	XOR	AL,AL
	XOR	CX,CX
	MOV	DX,24*256+79
	JMP	SHORT	CLW
ENDP
CLRSTR	PROC
	MOV	AL,1
	MOV	CX,23*256
	MOV	DX,23*256+79
ENDP
CLW	PROC
	PUSH	AX
	PUSH	BX
	PUSH	CX
	PUSH	DX
	MOV	AH,7
	MOV	BH,AH
	INT	10H
	POP	DX
	POP	CX
	POP	BX
	POP	AX
	RET
ENDP
COMPL	PROC
	PUSH	AX
	PUSH	DS
	MOV	DS,DATS
	CALL	CLS
	MOV	AX,900H+(80-21)/2
	CALL	AT
	LEA	SI,OPTIDI
	CALL	WW
	MOV	AX,DS:[CUR_DISK]
	ADD	AL,'A'
	CALL	WS
	MOV	AL,':'
	CALL	WS
	MOV	AX,0A00H+(80-19)/2
	CALL	AT
	LEA	SI,OPTIMZ
	CALL	WW
	POP	DS
	POP	AX
	RET
ENDP
CHANGE_DISK	PROC
	CALL	PR_MENU
CHANGE_DISK_A:
	CALL	INKEY
	SUB	AL,2
	CMP	AL,DS:[DISKS]
	JZ	CHANGE_DISK_EXIT
	JA	CHANGE_DISK_A
	XOR	AH,AH
	STC
CHANGE_DISK_EXIT:
	RET
ENDP
PR_MENU PROC
	MOV	DX,0A00H+(80-25)/2
	XOR	BX,BX
	XOR	CH,CH
	MOV	CL,DS:[DISKS]
PR_MENU_1:
	MOV	AX,DX
	CALL	AT
	MOV	AX,BX
	ADD	AL,31H
	CALL	WS
	LEA	SI,DISKSTR
	CALL	WW
	MOV	AX,BX
	ADD	AL,'A'
	CALL	WS
	LEA	SI,DBLPOI
	CALL	WW
	INC	BX
	INC	DH
	LOOP	PR_MENU_1
	MOV	AX,DX
	CALL	AT
	MOV	AX,BX
	ADD	AL,31H
	CALL	WS
	JMP	WW
ENDP
INKEY	PROC
	MOV	AX,0
	INT	16H
	XCHG	AL,AH
	RET
ENDP
DEFRAGP PROC
	CALL	READ_BOOT
	CALL	READ_FAT
	CALL	DEF_PRO
	RET
ENDP
READ_BOOT	PROC
	PUSH	AX
	MOV	DL,AL
	MOV	ES,BOOTS
	XOR	BX,BX
	MOV	AX,201H
	MOV	CX,BX
	MOV	DH,CL
	INC	CL
	INT	13H
	JNC	READ_BOOT_OK
	CALL	WRITE_ERROR
	POP	AX
	POP	AX
	POP	AX
	JMP	DEFRAG_I
READ_BOOT_OK:
	MOV	AX,ES:[BPB+11]
	MOV	BX,ES:[BPB]
	MUL	BX
	SHR	AX,4
	ROR	DX,4
	OR	AX,DX
	SHL	AX,1
	ADD	ROOTS,AX
	PUSH	BP
	MOV	BP,AX
	MOV	AX,ES:[BPB+6]
	SHL	AX,1
	ADD	AX,BP
	POP	BP
	ADD	BUFS,AX
	ADD	BUF1S,AX
	ADD	CLUSS,AX
	MOV	DS,DATS
	MOV	AL,ES:[54+4]
	SUB	AL,30H
	SHR	AL,2
	MOV	DS:[FAT_TYPE],AL
	MOV	AL,ES:[BPB+2]
	MOV	BYTE PTR DS:[CLUST_SIZE],AL
	MOV	BX,128
	XOR	AH,AH
	XCHG	AX,BX
	XOR	DX,DX
	DIV	BX
	MOV	DS:[MSCS],AX
	MOV	AX,ES:[BPB+11]
	SHL	AX,1
	ADD	DS:[RES_SEC],AX
	MOV	AX,ES:[BPB+3]
	ADD	DS:[RES_SEC],AX
	MOV	AX,ES:[BPB+6]
	SHL	AX,5
	XOR	DX,DX
	DIV	WORD PTR ES:[BPB]
	ADD	DS:[RES_SEC],AX
	MOV	AL,ES:[BPB+2]
	XOR	AH,AH
	SHL	AX,1
	SUB	DS:[RES_SEC],AX
	POP	AX
	RET
ENDP
READ_FAT	PROC
	PUSH	AX
	MOV	ES,BOOTS
	MOV	DL,AL
	MOV	CX,1
	ADD	CX,ES:[BPB+3]
	MOV	DH,CH
	MOV	AX,200H
	MOV	AL,ES:[BPB+11]
	XOR	BX,BX
	MOV	ES,FATS
	INT	13H
	JNC	READ_FAT_OK
	CALL	WRITE_ERROR
	POP	AX
	POP	AX
	POP	AX
	JMP	DEFRAG_I
READ_FAT_OK:
	POP	AX
	RET
ENDP
CHECK_USED	PROC
	PUSHA
	PUSH	DS
	PUSH	ES
	MOV	ES,DATS
	MOV	DS,FATS
	MOV	SI,4
	XOR	BP,BP
	MOV	CX,ES:[QUANT_CLUST]
	SUB	CX,2
CHECK_USED_1:
	LODSW
	OR	AX,AX
	JZ	CHECK_USED_NO
	CMP	AX,0FFF8H
	JAE	CHECK_USED_2
	CMP	AX,0FFF0H
	JAE	CHECK_USED_NO
CHECK_USED_2:
	INC	BP
CHECK_USED_NO:
	LOOP	CHECK_USED_1
	MOV	ES:[USED_CLUST],BP
	OR	BP,BP
	POP	ES
	POP	DS
	POPA
	RET
ENDP
DEF_PRO PROC
	CALL	OPTI_ROOT
	CALL	OPTI_DATA
	RET
ENDP
OPTI_ROOT	PROC
	CALL	READ_ROOT
	CALL	SORT_ROOT
	CALL	ZERO_ROOT
	RET
ENDP
READ_ROOT	PROC
	PUSH	DS
	MOV	DS,DATS
	MOV	ES,BOOTS
	MOV	AX,ES:[BPB+11]
	SHL	AX,1
	ADD	AX,ES:[BPB+3]
	CALL	SECT2CYL;(IN AX;OUT CH-CYL,CL-SECT,DH-HEAD)
	MOV	DL,BYTE PTR DS:[CUR_DISK]
	MOV	AX,ES:[BPB+6]
	SHR	AX,4
	PUSH	AX
	MOV	AH,2
	MOV	ES,ROOTS
	XOR	BX,BX
	INT	13H
	JNC	READ_ROOT_OK
	CALL	WRITE_ERROR
	POP	AX
	POP	DS
	POP	AX
	POP	AX
	POP	AX
	JMP	DEFRAG_I
READ_ROOT_OK:
	POP	AX
	MOV	DS,DATS
	MOV	BX,DS:[CLUST_SIZE]
	XOR	DX,DX
	DIV	BX
	MOV	DS:[CSC],AX
	POP	DS
	RET
ENDP
SECT2CYL	PROC
	PUSH	ES
	PUSH	DS
	PUSH	BX
	INC	AX
	PUSH	AX
	MOV	DS,DATS
	MOV	AX,DS:[CUR_DISK]
	MOV	DL,AL
	MOV	AX,800H
	INT	13H
	INC	DH
	XOR	CH,CH
	MOV	DL,CH
	XCHG	DL,DH
	MOV	AX,DX
	MUL	CX
	MOV	BX,AX
	POP	AX
	DIV	BX
	PUSH	AX
	MOV	AX,DX
	XOR	DX,DX
	DIV	CX
	PUSH	AX
	MOV	AX,DX
	POP	DX
	POP	CX
	MOV	CH,AL
	XCHG	CH,CL
	XCHG	DL,DH
	POP	BX
	POP	DS
	POP	ES
	RET
ENDP
SORT_ROOT	PROC
	PUSH	BP
	MOV	DS,BOOTS
	MOV	CX,DS:[BPB+6]
	MOV	DS,ROOTS
	PUSH	DS
	POP	ES
	CALL	CHECK_RECS;OUT	CX
	OR	CX,CX
	JZ	SORT_ROOT_EXIT
	DEC	CX
	MOV	BP,CX
SORT_ROOT_3:
	PUSH	CX
	MOV	CX,BP
	XOR	DI,DI
SORT_ROOT_2:
	MOV	SI,DI
	ADD	DI,32
	CALL	COMP_STRS
	JC	SORT_ROOT_1
	CALL	SWAP_STRS
SORT_ROOT_1:
	LOOP	SORT_ROOT_2
	DEC	BP
	POP	CX
	LOOP	SORT_ROOT_3
SORT_ROOT_EXIT:
	POP	BP
	RET
ENDP
CHECK_RECS	PROC
	PUSH	SI
	PUSH	DI
	XOR	AX,AX
	MOV	DI,AX
	MOV	SI,AX
CHECK_RECS_2:
	INC	SI
	SCASB
	JZ	CHECK_RECS_1
	ADD	DI,31
	LOOP	CHECK_RECS_2
CHECK_RECS_1:
	DEC	SI
	MOV	CX,SI
	POP	DI
	POP	SI
	RET
ENDP
COMP_STRS	PROC
	PUSH	CX
	PUSH	SI
	PUSH	DI
	MOV	CX,11
COMP_STRS_3:
	CMPSB
	JNZ	COMP_STRS_1
	LOOP	COMP_STRS_3
	CLC
COMP_STRS_1:
	POP	DI
	POP	SI
	POP	CX
	RET
ENDP
SWAP_STRS	PROC
	PUSH	CX
	PUSH	SI
	PUSH	DI
	MOV	CX,16
SWAP_STRS_1:
	LODSW
	XCHG	AX,ES:[DI]
	MOV	DS:[SI-2],AX
	ADD	DI,2
	LOOP	SWAP_STRS_1
	POP	DI
	POP	SI
	POP	CX
	RET
ENDP
ZERO_ROOT	PROC
	PUSH	SI
	PUSH	DI
	PUSH	DS
	PUSH	ES
	MOV	DS,BOOTS
	MOV	CX,DS:[BPB+6]
	MOV	DS,ROOTS
	PUSH	DS
	POP	ES
ZERO_ROOT_1:
	LODSB
	OR	AL,AL
	JZ	ZERO_ROOT_EXIT
	CMP	AL,0E5H
	JZ	ZERO_ROOT_2
ZERO_ROOT_3:
	ADD	SI,31
	LOOP	ZERO_ROOT_1
	JMP	SHORT	ZERO_ROOT_EXIT
ZERO_ROOT_2:
	PUSH	CX
	MOV	DI,SI
	DEC	DI
	MOV	CX,32
	XOR	AL,AL
	REP	STOSB
	POP	CX
	JMP	SHORT	ZERO_ROOT_3
ZERO_ROOT_EXIT:
	POP	ES
	POP	DS
	POP	DI
	POP	SI
	RET
ENDP
WRITE_ROOT	PROC
	PUSH	DS
	PUSH	ES
	MOV	ES,BOOTS
	MOV	DS,DATS
	MOV	AX,ES:[BPB+11]
	SHL	AX,1
	ADD	AX,ES:[BPB+3]
	CALL	SECT2CYL;(IN AX;OUT CH-CYL,CL-SECT,DH-HEAD)
	MOV	DL,BYTE PTR DS:[CUR_DISK]
	MOV	AX,ES:[BPB+6]
	SHR	AX,4
	MOV	AH,3
	MOV	ES,ROOTS
	XOR	BX,BX
	INT	13H
	JNC	WRITE_ROOT_OK
	CALL	WRITE_ERROR
	MOV	SP,100H
	JMP	DEFRAG_I
WRITE_ROOT_OK:
	POP	ES
	POP	DS
	RET
ENDP
MAXIMIZE_FAT	PROC
	PUSHF
	PUSHA
	PUSH	DS
	PUSH	ES
	MOV	DS,BOOTS
	MOV	AX,DS:[BPB+11]
	MUL	WORD PTR DS:[BPB]
	MOV	SI,AX
	MOV	BX,3
	DIV	BX
	SHL	AX,2
	MOV	DI,AX
	SHR	AX,1
	MOV	CX,AX
	DEC	CX
	SHR	CX,1
	MOV	DS,DATS
	MOV	DS:[QUANT_CLUST],CX
	DEC	DI
	DEC	SI
	DEC	DI
	DEC	SI
	MOV	DS,FATS
	PUSH	DS
	POP	ES
	STD
MAXIMIZE_FAT_1:
	LODSW
	SAR	AX,4
	STOSW
	INC	SI
	LODSW
	SHL	AX,4
	SAR	AX,4
	STOSW
	LOOP	MAXIMIZE_FAT_1
	POP	ES
	POP	DS
	POPA
	POPF
	RET
ENDP
MINIMIZE_FAT	PROC
	PUSHF
	PUSHA
	PUSH	DS
	PUSH	ES
	MOV	DS,BOOTS
	MOV	AX,DS:[BPB+11]
	MUL	WORD PTR DS:[BPB]
	MOV	BX,3
	DIV	BX
	SHL	AX,1
	MOV	CX,AX
	DEC	CX
	SHR	CX,1
	MOV	SI,4
	MOV	DI,3
	MOV	DS,FATS
	PUSH	DS
	POP	ES
	CLD
MINIMIZE_FAT_1:
	LODSW
	AND	AX,0FFFH
	STOSW
	DEC	DI
	LODSW
	SHL	AX,4
	OR	AL,ES:[DI]
	STOSW
	LOOP	MINIMIZE_FAT_1
	POP	ES
	POP	DS
	POPA
	POPF
	RET
ENDP
EXT_FAT PROC
	PUSH	DS
	MOV	DS,DATS
	MOV	AL,DS:[FAT_TYPE]
	OR	AL,AL
	JNZ	EXT_FAT_1
	CALL	MAXIMIZE_FAT
EXT_FAT_1:
	POP	DS
	RET
ENDP
OPTI_DATA	PROC
	MOV	DS,DATS
	CALL	EXT_FAT
	CALL	CHECK_USED
	JZ	NO_OPTI
	CALL	COMPL
	MOV	DX,2
OPTI_DATA_2:
	CALL	READ_NEXT_FILE_CLUSTER;(AX -CLUSTER,C=0 NO MORE FILES)
	JNC	OPTI_DATA_4
	OR	AX,AX
	JZ	OPTI_DATA_2
OPTI_DATA_3:
	XCHG	AX,DX
	CALL	CHECK_BAD
	JC	OPTI_DATA_5
	CALL	SWAP_CLUSTERS;IN AX=2,DX=FILE CLUSTER ;OUT DX=2
	CALL	PRINT_PERCENTS
	;AX =FILE CLUSTER;BX=NEXTFILECLUSTER
	;BX>=FFF8H,END
	MOV	AX,BX
OPTI_DATA_5:
	INC	DX
	CMP	AX,0FFF8H
	JAE	OPTI_DATA_2
	JMP	SHORT	OPTI_DATA_3
OPTI_DATA_4:
	CALL	UPDATE_FAT
	MOV	DS,DATS
	MOV	AX,0B00H+(80-21)/2
	CALL	AT
	LEA	SI,OPTI_COMPL
	CALL	WW
	RET
NO_OPTI:
	MOV	DS,DATS
	CALL	CLS
	MOV	AX,0A00H+(80-33)/2
	CALL	AT
	LEA	SI,NO_OPTIMZ
	CALL	WW
	MOV	AX,DS:[CUR_DISK]
	ADD	AL,'A'
	CALL	WS
	CALL	WRITE_ROOT
	RET
ENDP
PRINT_PERCENTS	PROC
	PUSHA
	PUSH	DS
	MOV	DS,DATS
	MOV	BX,100
	MOV	AX,DS:[CURRENT_CLUST]
	MUL	BX
	DIV	DS:[USED_CLUST]
	PUSH	AX
	MOV	AX,0A00H+(80+11)/2
	CALL	AT
	POP	AX
	CALL	WH2D
	MOV	AL,'%'
	CALL	WS
	POP	DS
	POPA
	RET
ENDP
CHECK_BAD	PROC
	CALL	READ_DOWNLINKS
	CMP	CX,0FFF0H
	JB	CHECK_BAD_1
	CMP	CX,0FFF7H
	JA	CHECK_BAD_1
	STC
	XCHG	AX,DX
	RET
CHECK_BAD_1:
	CLC
	RET
ENDP
READ_NEXT_FILE_CLUSTER	PROC
	PUSH	DX
	PUSH	DS
	PUSH	ES
	MOV	DS,DATS
	PUSH	DS
	POP	ES
	MOV	AX,DS:[CURRENT_FILE]
	SHL	AX,5
	MOV	DS,ROOTS
	MOV	SI,AX
	LODSB
	OR	AL,AL
	JZ	READ_NEXT_FILE_CLUSTER_EXIT
	ADD	SI,25
	LODSW
	INC	ES:[CURRENT_FILE]
	STC
READ_NEXT_FILE_CLUSTER_EXIT:
	POP	ES
	POP	DS
	POP	DX
	RET
ENDP
SWAP_CLUSTERS	PROC;AX-SELECT;DX-FILE
	PUSH	AX
	PUSH	CX
	PUSH	DX
	PUSH	SI
	PUSH	DI
	PUSH	DS
	PUSH	ES
	CMP	AX,DX
	JE	SWAP_CLUSTERS_1
	CALL	SWAP_UPLINKS;IN AX-CSL,DX-CFL
	CALL	READ_DOWNLINKS;OUT CX-FROM CSL,BX-FROM CFL
	CMP	BX,AX
	JNZ	SWAP_CLUSTERS_2
	MOV	BX,DX
SWAP_CLUSTERS_2:
	CALL	UPDATE_DOWNLINKS;IN BX-CLU FOR CSL,IN CX-CLU FOR CFL
	CALL	REWRITE_CLUSTERS
SWAP_CLUSTERS_EXIT:
	POP	ES
	POP	DS
	POP	DI
	POP	SI
	POP	AX
	POP	CX
	POP	DX
	PUSH	DS
	MOV	DS,DATS
	INC	DS:[CURRENT_CLUST]
	POP	DS
	RET
SWAP_CLUSTERS_1:
	CALL	READ_DOWNLINK
	JMP	SHORT	SWAP_CLUSTERS_EXIT
ENDP
READ_DOWNLINKS	PROC;IN AX-CSL,DX-CFL;OUT BX-FROM CFL;CX-FROM CSL
	PUSH	AX
	CALL	READ_DOWNLINK
	MOV	CX,BX
	MOV	AX,DX
	CALL	READ_DOWNLINK
	POP	AX
	RET
ENDP
READ_DOWNLINK		PROC;IN AX;OUT AX-SAME,BX
	PUSH	AX
	PUSH	SI
	PUSH	DS
	MOV	DS,FATS
	SHL	AX,1
	MOV	SI,AX
	LODSW
	MOV	BX,AX
	POP	DS
	POP	SI
	POP	AX
	RET
ENDP
UPDATE_DOWNLINKS	PROC;IN AX-CSL,DX-SFL,BX-FOR CSL,CX-FOR CFL
	PUSH	AX
	PUSH	BX
	CALL	UPDATE_DOWNLINK
	MOV	AX,DX
	MOV	BX,CX
	CALL	UPDATE_DOWNLINK
	POP	BX
	POP	AX
	RET
ENDP
UPDATE_DOWNLINK     PROC
	PUSH	AX
	PUSH	DI
	PUSH	ES
	MOV	ES,FATS
	SHL	AX,1
	MOV	DI,AX
	MOV	AX,BX
	STOSW
	POP	ES
	POP	DI
	POP	AX
	RET
ENDP
SWAP_UPLINKS	PROC
	PUSH	AX
	PUSH	DX
	PUSH	BP
	XOR	BP,BP
	CALL	SWAP_IN_FAT
	CALL	SWAP_IN_ROOT
	POP	BP
	POP	DX
	POP	AX
	RET
ENDP
SWAP_IN_ROOT	PROC;IN AX ,DX
	PUSH	BX
	PUSH	SI
	PUSH	DS
	XOR	SI,SI
	MOV	DS,ROOTS
	MOV	BX,AX
SWAP_IN_ROOT_1:
	LODSB
	OR	AL,AL
	JZ	SWAP_IN_ROOT_EXIT
	ADD	SI,25
	LODSW
	ADD	SI,4
	OR	AX,AX
	JZ	SWAP_IN_ROOT_1
	CMP	AX,BX
	JZ	SWAP_IN_ROOT_2
	CMP	AX,DX
	JNZ	SWAP_IN_ROOT_1
	TEST	BP,1
	JNZ	SWAP_IN_ROOT_1
	MOV	DS:[SI-6],BX
	OR	BP,1
	JMP	SHORT	SWAP_IN_ROOT_1
SWAP_IN_ROOT_2:
	TEST	BP,2
	JNZ	SWAP_IN_ROOT_1
	MOV	DS:[SI-6],DX
	OR	CL,2
	JMP	SHORT	SWAP_IN_ROOT_1
SWAP_IN_ROOT_EXIT:
	MOV	AX,BX
	POP	DS
	POP	SI
	POP	BX
	RET
ENDP
SWAP_IN_FAT	PROC;IN AX ,DX
	PUSH	BX
	PUSH	CX
	PUSH	SI
	PUSH	DI
	PUSH	DS
	MOV	DS,DATS
	MOV	CX,DS:[QUANT_CLUST]
	MOV	DS,FATS
	MOV	SI,4
	MOV	BX,AX
	MOV	DI,1
SWAP_IN_FAT_1:
	INC	DI
	LODSW
	CMP	AX,BX
	JZ	SWAP_IN_FAT_2
	CMP	AX,DX
	JZ	SWAP_IN_FAT_4
	LOOP	SWAP_IN_FAT_1
	JMP	SHORT	SWAP_IN_FAT_3
SWAP_IN_FAT_2:
	CMP	DI,DX
	JZ	SWAP_IN_FAT_5
	MOV	DS:[SI-2],DX
SWAP_IN_FAT_5:
	OR	BP,2
	JMP	SHORT	SWAP_IN_FAT_1
SWAP_IN_FAT_4:
	MOV	DS:[SI-2],BX
	OR	BP,1
	JMP	SHORT	SWAP_IN_FAT_1
SWAP_IN_FAT_3:
	MOV	AX,BX
	POP	DS
	POP	DI
	POP	SI
	POP	CX
	POP	BX
	RET
ENDP
REWRITE_CLUSTERS	PROC
	XCHG	AX,DX
	PUSH	AX
	CALL	READ_CLUSTER1;IN AX -CLU
	MOV	AX,DX
	CALL	READ_CLUSTER2;IN AX -CLU
	CALL	WRITE_CLUSTER1;IN AX -CLU
	POP	AX
	CALL	WRITE_CLUSTER2;IN AX -CLU
	RET
ENDP
UPDATE_FAT	PROC
	MOV	DS,DATS
	MOV	AL,DS:[FAT_TYPE]
	OR	AL,AL
	JNZ	UPDATE_FAT_1
	CALL	MINIMIZE_FAT
UPDATE_FAT_1:
	CALL	WRITE_FAT
	CALL	WRITE_ROOT
	RET
ENDP
READ_CLUSTER1	PROC
	PUSH	BX
	PUSH	ES
	MOV	ES,BUFS
	CALL	READ_CLUSTER
	POP	ES
	POP	BX
	RET
ENDP
READ_CLUSTER2	PROC
	PUSH	BX
	PUSH	ES
	MOV	ES,BUF1S
	CALL	READ_CLUSTER
	POP	ES
	POP	BX
	RET
ENDP
WRITE_CLUSTER1	PROC
	PUSH	BX
	PUSH	ES
	MOV	ES,BUFS
	CALL	WRITE_CLUSTER
	POP	ES
	POP	BX
	RET
ENDP
WRITE_CLUSTER2	PROC
	PUSH	BX
	PUSH	ES
	MOV	ES,BUF1S
	CALL	WRITE_CLUSTER
	POP	ES
	POP	BX
	RET
ENDP
WRITE_CLUSTER	PROC
	PUSHA
	PUSH	DS
WRITE_CLUSTER_1:
	PUSH	AX
	MOV	DS,DATS
	MUL	WORD PTR DS:[CLUST_SIZE]
	ADD	AX,DS:[RES_SEC]
	ADC	DX,0
	MOV	CX,AX
	MOV	AX,DS:[CUR_DISK]
	LEA	BX,DAT:TABL_DISK
	MOV	DS:[BX+6+2],ES
	MOV	DS:[BX],CX
	MOV	DS:[BX+2],DX
	MOV	CX,0FFFFH
	INT	26H
	POP	AX
	POP	AX
	JC	WRITE_CLUSTER_ERR
	POP	DS
	POPA
	RET
WRITE_CLUSTER_ERR:
	PUSH	AX
	MOV	DS,DATS
	MOV	AX,23*256+(80-37)/2
	CALL	AT
	LEA	SI,PRESSA
	CALL	WW
	CALL	INKEY
	POP	AX
	JMP	SHORT	WRITE_CLUSTER_1
ENDP
READ_CLUSTER	PROC
	PUSHA
	PUSH	DS
READ_CLUSTER_1:
	PUSH	AX
	MOV	DS,DATS
	MUL	WORD PTR DS:[CLUST_SIZE]
	ADD	AX,DS:[RES_SEC]
	ADC	DX,0
	MOV	CX,AX
	MOV	AX,DS:[CUR_DISK]
	LEA	BX,DAT:TABL_DISK
	MOV	DS:[BX+6+2],ES
	MOV	DS:[BX],CX
	MOV	DS:[BX+2],DX
	MOV	CX,0FFFFH
	INT	25H
	POP	AX
	POP	AX
	JC	READ_CLUSTER_ERR
	POP	DS
	POPA
	RET
READ_CLUSTER_ERR:
	PUSH	AX
	MOV	DS,DATS
	MOV	AX,23*256+(80-37)/2
	CALL	AT
	LEA	SI,PRESSA
	CALL	WW
	CALL	INKEY
	CALL	CLRSTR
	POP	AX
	JMP	SHORT	READ_CLUSTER_1
ENDP
WRITE_FAT	PROC
	PUSH	AX
	MOV	DS,DATS
	MOV	ES,BOOTS
	MOV	DL,AL
	MOV	CX,1
	ADD	CX,ES:[BPB+3]
	MOV	DH,CH
	MOV	AX,300H
	MOV	AL,ES:[BPB+11]
	XOR	BX,BX
	PUSH	BX
	PUSH	ES
	MOV	ES,FATS
	INT	13H
	JNC	WRITE_FAT_OK
	CALL	WRITE_ERROR
	MOV	SP,100H
	JMP	DEFRAG_I
WRITE_FAT_OK:
	POP	ES
	POP	BX
	MOV	AX,ES:[BPB+3]
	ADD	AX,ES:[BPB+11]
	CALL	SECT2CYL
	MOV	AL,ES:[BPB+11]
	MOV	AH,3
	MOV	DL,BYTE PTR DS:[CUR_DISK]
	MOV	ES,FATS
	INT	13H
	JNC	WRITE_FAT_OK1
	CALL	WRITE_ERROR
	MOV	SP,100H
	JMP	DEFRAG_I
WRITE_FAT_OK1:
	POP	AX
	RET
ENDP
include ..\..\library\text\ww.lib
INCLUDE ..\..\LIBRARY\TEXT\CURsor.LIB
INCLUDE ..\..\LIBRARY\TEXT\WH2D.LIB
DATSEG	DW	DAT
BOOTSEG DW	BOOT
FATSEG	DW	FAT
ROOTSEG DW	ROOT
BUFSEG	DW	BUF
BUF1SEG DW	BUF1
CLUSSEG DW	CLUS
ENDS
DAT	SEGMENT PARA
DISKS	DB	?
CUR_DISK	DW	?
FAT_TYPE	DB	?
RES_SEC DW	0
CURRENT_SELECTOR_CLUSTER	DW	0
CSC	EQU	CURRENT_SELECTOR_CLUSTER
CURRENT_FILE_CLUSTER	DW     0
CFC	EQU	CURRENT_FILE_CLUSTER
MAX_SEG_CLUSTER_SIZE	DW     0;-?
MSCS	EQU	MAX_SEG_CLUSTER_SIZE
CLUST_SIZE	DW	0
LEN_READ_FILE	DW	0
CURRENT_RECORD	DW	0
CURRENT_FILE	DW	0
CURRENT_CLUST	DW	0
QUANT_CLUST	DW	?
USED_CLUST	DW	?
TABL_DISK	DW	?,?
		DW	1
		DW	0
		DW	?
DISKSTR DB	'..Оптимизировать диск ',0
DBLPOI	DB	':',0
EXITPRO DB	'..Выход',0
NO_DISKS_	DB	'Не найдено гибких дисков',0
OPTIMZ		DB	'Оптимизировано ',0
OPTIDI		DB	'Оптимизируется диск ',0
NO_OPTIMZ	DB	'Нечего оптимизировать на диске ',0
OPTI_COMPL	DB	'Оптимизация завершена',0
NOT_READY	DB	'Диск не готов',0
PRESSA		DB	'Нажмите любую клавишу для продолжения',0
ENDS
BOOT	SEGMENT PARA
ENDS
END     DEFRAG